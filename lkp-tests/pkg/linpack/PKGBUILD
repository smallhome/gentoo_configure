# Contributor: graysky <graysky AT archlinux dot us>

# set this to a null to omit the docs and licenses
_installdocs=yes

pkgname=('linpack')
pkgver=11.2.1.009
_pkgvershort=11.2.1
_number=4942
pkgrel=2
arch=('i686' 'x86_64')
pkgdesc='Benchmark based on linear algebra excellent app for stress testing.'
license=('custom')
url="http://software.intel.com/en-us/articles/intel-math-kernel-library-linpack-download"
source=("http://registrationcenter.intel.com/irc_nas/$_number/l_lpk_p_$pkgver.tgz"
'linpack.conf' 'runme.template')
install=readme.install
backup=(etc/linpack.conf)
sha256sums=('51e98ea54638bed00d0733f68cefaf50205f397e80e7e760265ec09a5ab02c52'
            'd7e31c8c7e2648c8fa3c4838d98dfc39b8fadf0f74bdaa4ca52cd38277ec8254'
            '8c880adeee3de6c0e6005e210cea0e01b471219c590e25e824298a25ab0eef31')

build() {
	[[ -z "$CARCH" ]] && echo "Cannot determine architecture of this system!" && exit 1
	[[ "$CARCH" = "x86_64" ]] && bit=64 || bit=32
	export bit
	
	# setup based on detected arch
	sed -e "s/@TARGET_BIT@/$bit/g" <runme.template >linpack_runme_xeon$bit
}

package_linpack() {
	_path="$srcdir/l_lpk_p_$pkgver/linpack_$_pkgvershort"

	if [[ -n "$_installdocs" ]]; then
		# licenses
		install -Dm644 "$_path/doc/lpkEULA.txt" "$pkgdir/usr/share/licenses/linpack/lpkEULA.txt"
		install -Dm644 "$_path/doc/lpksupport.txt" "$pkgdir/usr/share/licenses/linpack/lpksupport.txt"
		
		# docs
		install -d "$pkgdir/usr/share/doc/linpack"
		cp -r "$_path/doc/mkl_userguide" "$pkgdir/usr/share/doc/linpack"
	fi
	
	#	config files
	install -Dm755 "$srcdir/linpack_runme_xeon$bit" "$pkgdir/usr/bin/linpack_runme_xeon$bit"
	install -Dm644 "$srcdir/linpack.conf" "$pkgdir/etc/linpack.conf"
	
	# bins
	install -Dm755 "$_path/benchmarks/linpack/xlinpack_xeon$bit" "$pkgdir/usr/bin/xlinpack_xeon$bit"
	install -Dm644 "$_path/benchmarks/linpack/xhelp.lpk" "$pkgdir/usr/share/doc/${pkgname}/xhelp.lpk"
}
